---
title: Integrate Geoeconomics datasets
author: Vincent Emonet
authorURL: https://vemonet.github.io/
---


Generate a RDF knowledge graph integrating geographical and economics data from **[WorldBank](https://data.worldbank.org/indicator/NY.GDP.MKTP.CD)** and **[GeoNames](https://www.geonames.org/)**. Using Linux or MacOS is recommended as the [d2s tool](https://pypi.org/project/d2s/) support for Windows is still a work in progress.

<!--truncate-->

## Install the d2s tool

See the [complete installation documentation](https://d2s.semanticscience.org/docs/d2s-installation) to install pip, [pipx](https://pipxproject.github.io/pipx/) and Docker using Chocolatey. Then  install the [d2s tool](https://pypi.org/project/d2s/) using pipx:

```shell
pipx install d2s cwlref-runner
d2s init my-project-folder
cd my-project-folder
```

> We recommend using [pipx](https://pipxproject.github.io/pipx/), but `pip` would work too.

## Generate datasets

Generate the WorldBank and GeoNames datasets:

```shell
d2s generate dataset
# Follow prompt instructions to set the dataset metadata
d2s generate dataset
```

The folder with the RML mappings, metadata and download files are generated in `datasets/worldbank` and `datasets/geonames`

## Define download files

Define a simple bash script to download WorldBank XML from its GitHub URL in `datasets/worldbank/download/download.sh`:

```shell
#!/bin/bash
## Download XML file from GitHub
wget -N https://raw.githubusercontent.com/MaastrichtU-IDS/UM_KEN4256_KnowledgeGraphs/master/dataset-worldbank-gdp-full.xml
wget -N https://raw.githubusercontent.com/MaastrichtU-IDS/UM_KEN4256_KnowledgeGraphs/master/dataset-worldbank-gdp.xml
```

Same for GeoNames:

```shell
#!/bin/bash
## Download TSV file from GitHub
wget -N https://raw.githubusercontent.com/MaastrichtU-IDS/UM_KEN4256_KnowledgeGraphs/master/dataset-geonames-countryInfo.csv
```

## Improve metadata

We improved the metadata for GeoNames and WorldBank datasets by editing those files:

* `datasets/worldbank/metadata/metadata-template-0-summary.rq`
* `datasets/worldbank/metadata/metadata-template-1.rq`
* `datasets/geonames/metadata/metadata-template-0-summary.rq`
* `datasets/geonames/metadata/metadata-template-1.rq`

> To edit metadata such as keywords, theme, homepage, download URL, etc.

## Define RML mapping files

Some [templates mapping files](https://github.com/MaastrichtU-IDS/d2s-cwl-workflows/tree/master/support/template/dataset/mapping) are generated when creating a new dataset to help start mappings different king of file. Delete them and define mapping files for WorldBank and GeoName.

* WorldBank mappings in [datasets/worldbank/mapping/gdp_mapping.rml.ttl](https://github.com/MaastrichtU-IDS/d2s-transform-template/blob/master/datasets/worldbank/mapping/gdp_mapping.rml.ttl)
* GeoName mappings in [datasets/geonames/mapping/country_mapping.rml.ttl](https://github.com/MaastrichtU-IDS/d2s-transform-template/blob/master/datasets/geonames/mapping/country_mapping.rml.ttl)
* Additional mapping file to map WorldBank and GeoNames using ISO2 and ISO3 code correspondance in [datasets/geonames/mapping/iso_mapping.rml.ttl](https://github.com/MaastrichtU-IDS/d2s-transform-template/blob/master/datasets/geonames/mapping/iso_mapping.rml.ttl)

> More resources around RML mappings can be found at https://rml.io/, such as [specifications and tutorials](https://rml.io/docs/).

## Download input files

Use the `d2s` tool to download datasets files defined in their respective `download/download.sh` file:

```shell
d2s download worldbank
d2s download geonames
```

Here is a sample of the CSV file used to map ISO2 and ISO3:

```
ISO,ISO3
AD,AND
AE,ARE
```

## Run the RMLMapper

Fix input file permissions:

```shell
sudo chmod -R 777 workspace/input 
```

Start the data transformation

```shell
d2s rml worldbank --mapper
d2s rml geonames --mapper --watch 
```

> Ntriple files generated in `workspace/import`

The `RMLStreamer` can also be used, it is faster and more scalable, but its development is not has advanced as the rmlmapper and in this use-case it is facing issues with RML conversion of WorldBank. So we used the RMLMapper which properly generates the triples, but takes longer (about 40min on my laptop).

Instructions to run the RMLStreamer:

 ```shell
d2s start rmlstreamer rmltask
d2s rml worldbank --streamer --watch
d2s rml geonames --streamer --watch
 ```

> See running jobs at http://localhost:8078

## Load the generated triples

Start Ontotext GraphDB triplestore locally.

This can be done using `d2s` if you previously downloaded and set the GraphDB installation file.

```shell
d2s start graphdb
```

* Go to http://localhost:7200
  * Create a repository (e.g. `geoeconomics`). Enable `Use context index`.
* The files generated by the RML mapper should be available at http://localhost:7200/import#server
  * For the KG course we would have load the ntriples generated by the RML mappings for GeoNames and WorldBank in the "Named graph" http://kg-course/mapping
  * But in this use case we will load WorldBank and GeoNames data in separated graphs to be able to compute metadata about them.
    * https://w3id.org/d2s/graph/worldbank
    * https://w3id.org/d2s/graph/geonames
  * Load the ntriples generated by the ISO mappings in the Named graph http://kg-course/interlinking

## Browse the built Knowledge Graph

The [into-the-graph Web UI](https://d2s.semanticscience.org/docs/services-interfaces#into-the-graph) can be used to browse the SPARQL endpoint hosting the generated RDF Knowledge Graph.

Access the Knowledge Graph we built at http://trek.semanticscience.org/describe?uri=https://w3id.org/d2s/graph/geonames&endpoint=https://graphdb.dumontierlab.com/repositories/geoeconomics

> Make sure the SPARQL endpoint URL is set on [https://graphdb.dumontierlab.com/repositories/geoeconomics](http://trek.semanticscience.org/describe?uri=https://w3id.org/d2s/graph/geonames&endpoint=https://graphdb.dumontierlab.com/repositories/geoeconomics)

## Optional: compute and load metadata

Edit the config.yml to define the parameters of the workflow that will compute metadata for the dataset

* `sparql_final_graph_uri`: your graph
  * e.g. https://w3id.org/d2s/graph/geonames

* `sparql_final_triplestore_url`: SPARQL endpoint to compute and load the RDF 
  * e.g. http://graphdb:7200/repositories/geoeconomics/statements
* `sparql_final_triplestore_username`: triplestore username with Insert permissions.
  * e.g. admin
* `sparql_final_triplestore_password`: triplestore password
  * e.g. password

Fix permissions: `d2s update --permissions`

Run the [CWL workflows](https://www.commonwl.org/) (not supported on Windows at the moment):

```shell
d2s run compute-hcls-metadata.cwl worldbank
d2s run compute-hcls-metadata.cwl geonames
```

## Some SPARQL queries

From IDS KG course.

### Population and area comparison

List all countries with population less than 50,000 and order them from the smallest to the largest in terms of landmass area (square kilometres) [Geonames]

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
select ?countryUri ?countryLabel ?population ?area where {
  ?countryUri a gn:Country ;
      rdfs:label ?countryLabel ;
      dbo:areaTotal ?area ;
      dbo:populationTotal ?population .
  FILTER (xsd:integer(?population) < 50000)
} ORDER BY xsd:double(?area)
```

### Highest GDP in 2017

List the countries with the top 10 highest GDP values in 2017 [WorldBank and GeoNames]

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
SELECT * WHERE {
    ?gdp17 a dbr:Gross_domestic_product ;
        dbp:gdpNominalYear "2017"^^xsd:gYear ;
        dbp:gdpNominal ?gdpValue ;
        dbo:country ?worldbankCountry .
    ?worldbankCountry rdfs:label ?countryLabel .
    ?geonamesCountry owl:sameAs ?worldbankCountry ;
} ORDER BY DESC(?gdpValue) LIMIT 10
```

> We map WorldBank countries with GeoNames countries on owl:sameAs to filter out non-countries entities, such as World (WLD) and OECD members (OED).

### Highest GDP increase

List the countries with the top 10 highest **increases** in GDP between 1960 and 2017 [WorldBank and GeoNames].

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
SELECT DISTINCT ?countryLabel ((?gdpValue2017-?gdpValue1960) AS ?gdpIncrease) ?worldbankCountry
WHERE {
    ?gdp2017 a dbr:Gross_domestic_product ;
        dbp:gdpNominalYear "2017"^^xsd:gYear ;
        dbp:gdpNominal ?gdpValue2017 ;
        dbo:country ?worldbankCountry .

    ?gdp1960 a dbr:Gross_domestic_product ;
        dbp:gdpNominalYear "1960"^^xsd:gYear ;
        dbp:gdpNominal ?gdpValue1960 ;
        dbo:country ?worldbankCountry .

    ?worldbankCountry rdfs:label ?countryLabel .
    ?geonamesCountry owl:sameAs ?worldbankCountry ;
    FILTER(str(?gdpValue1960) != "")
    FILTER(str(?gdpValue2017) != "")
} ORDER BY DESC(?gdpIncrease) LIMIT 10
```

> We filter out WorldBank countries without GDP values for wanted years and that are not countries using GeoNames mappings, and compute we the GDP increase in the Select statement.

### Most GDP increase in continents

For each continent, count the number of countries in that continent that are in the top 20 for highest increases in GDP between 1960 and 2017. Your answer should contain 2 values: the continent and the number of countries from the top 20 that are located in those continents (For example, Asia - 15, Europe - 5)

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
SELECT ?continent (count(?continent) AS ?countryCount)
WHERE {
  {
       SELECT DISTINCT ?worldbankCountry ?continent ((?gdpValue2017-?gdpValue1960) AS ?gdpIncrease)
       WHERE {
          ?gdp2017 a dbr:Gross_domestic_product ;
              dbp:gdpNominalYear "2017"^^xsd:gYear ;
              dbp:gdpNominal ?gdpValue2017 ;
              dbo:country ?worldbankCountry .

          ?gdp1960 a dbr:Gross_domestic_product ;
              dbp:gdpNominalYear "1960"^^xsd:gYear ;
              dbp:gdpNominal ?gdpValue1960 ;
              dbo:country ?worldbankCountry .

        ?geonamesCountry owl:sameAs ?worldbankCountry ;
            dbo:continent ?continent .
        FILTER(str(?gdpValue1960) != "")
        FILTER(str(?gdpValue2017) != "")
      } ORDER BY DESC(?gdpIncrease) LIMIT 20
  }
} GROUP BY ?continent ORDER BY DESC(?countryCount)
```

> We include the previous SPARQL query in a select subquery with a LIMIT 20, then group by on continent, and compute the country count in the select statement. 

### Construct GDP per capita

Construct the triples representing the GDP per capita for each country in 2017 [Geonames, WorldBank]

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
CONSTRUCT { 
    ?worldbankCountry dbp:gdpPerCapita ?gdpPerCapita .
} WHERE {
    ?gdp2017 a dbr:Gross_domestic_product ;
              dbp:gdpNominalYear "2017"^^xsd:gYear ;
              dbp:gdpNominal ?gdpValue2017 ;
              dbo:country ?worldbankCountry .
    ?country rdfs:label ?countryLabel .
    ?geonamesCountry owl:sameAs ?worldbankCountry ;
        dbo:populationTotal ?population .
   
    BIND( (?gdpValue2017/?population) AS ?gdpPerCapita )
}
```

> The GDP per capita calculation is done inside a BIND instead of the Select statement (not required, just for example purpose)

### Insert GDP per capita

Directly insert the triples representing the GDP per capita for each country in 2017, into your triplestore on GraphDB in the graph namespace “http://kg-course/query” [Geonames, WorldBank]

```SPARQL
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX dbo: <http://dbpedia.org/ontology/>
PREFIX dbr: <http://dbpedia.org/resource/>
PREFIX dbp: <http://dbpedia.org/property/>
PREFIX gn: <http://www.geonames.org/ontology#>
PREFIX wb: <http://data.worldbank.org/>
INSERT {
    GRAPH <http://kg-course/query> { 
        ?worldbankCountry dbp:gdpPerCapita ?gdpPerCapita 
    }
} WHERE {
    ?gdp2017 a dbr:Gross_domestic_product ;
              dbp:gdpNominalYear "2017"^^xsd:gYear ;
              dbp:gdpNominal ?gdpValue2017 ;
              dbo:country ?worldbankCountry .
    ?country rdfs:label ?countryLabel .
    ?geonamesCountry owl:sameAs ?worldbankCountry ;
        dbo:populationTotal ?population .
    BIND( (?gdpValue2017/?population) AS ?gdpPerCapita )
}
```

> In an INSERT query the graph can be defined in the Insert block, which is not possible for a Construct which generates only N-triples. Make sure you have correct permissions to Insert the statements in the queried SPARQL endpoint.